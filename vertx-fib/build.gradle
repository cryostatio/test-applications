plugins {
    id 'java'
    id 'application'
    id 'com.google.cloud.tools.jib' version '3.4.2'
    id 'com.ryandens.javaagent-jib' version '0.6.1'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
      url 'https://central.sonatype.com/repository/maven-snapshots/'
    }
}

dependencies {
    implementation 'io.vertx:vertx-web:4.5.8'
    javaagent "io.cryostat:cryostat-agent:${System.getenv('CRYOSTAT_AGENT_VERSION')}:shaded"
}

task printVersion {
  doLast {
    println(project.findProperty('version'))
  }
}

mainClassName = 'es.andrewazor.demo.Main'

def baseImage = 'registry.access.redhat.com/ubi8/openjdk-17';
def baseImageTag = 'latest';

def buildArch = System.getenv('JIB_ARCH') ?: 'amd64'
def buildOs = 'linux'

def appRegistry = System.getenv('APP_REGISTRY') ?: 'quay.io'
def appNamespace = System.getenv('APP_NAMESPACE') ?: 'redhat-java-monitoring'
def appName = System.getenv('APP_NAME') ?: 'vertx-cryostat-agent'

jib {
    from {
        image = baseImage + ':' + baseImageTag
        platforms {
            platform {
                architecture = buildArch
                os = buildOs
            }
        }
    }
    to {
        image = appRegistry + '/' + appNamespace + '/' + appName
        tags = [buildOs + '-' + buildArch]
    }
    dockerClient {
        executable = '/usr/bin/podman'
    }
    container {
        mainClass = mainClassName
        creationTime = 'USE_CURRENT_TIMESTAMP'
        entrypoint = '/app/entrypoint.sh'
        ports = [
        '8080',
        '9093',
        ]
    }
    extraDirectories {
        paths = 'src/main/extras'
        permissions = [
            '/app/entrypoint.sh': '755',
            '/app/resources/jmxremote.password.in': '440',
            '/app/resources/jmxremote.access.in': '440',
        ]
    }
}

applicationDefaultJvmArgs = jib.container.jvmFlags
